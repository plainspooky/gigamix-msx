;とーくまんによるＰＣＭ再生ルーチン
;(Ｃ)１９９４，１９９５　ＧＩＧＡＭＩＸ／ごりぽん／いたろう

;ラベル＆シンボル定義

VDP0	EQU	98H
VDP1	EQU	99H
RDVDP	EQU	0006H
;NEWCPU	EQU	04306H
;CPUCHG	EQU	04022H

;	ORG	03000H
	ORG	XPCMDR

;		*----+----*----+----*----+----*----+----*
	DEFM	'PCM DRIVER for TALKMAN  BY GORIPON/ITARO'
	DEFB	1AH
	DEFB	01H	;PCM Driver ID
	DEFS	6

	JP	PCMPLY

PCMPLY:	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL,(RDVDP)
	INC	H
	LD	A,L
	LD	(SWVDP0+1),A
	LD	A,H
	LD	(SWVDP1+1),A

	LD	A,(NEWCPU)
	LD	(CPUMOD),A
	XOR	A
	CALL	CPUCHG

	POP	HL
	POP	DE
	POP	BC
	POP	AF

	DI
	PUSH	BC
	PUSH	DE
	PUSH	HL

	LD	B,A
	AND	3
	JR	Z,RATE15
	CP	2
	JR	C,RATE7
	JR	Z,RATE5
RATE3:	LD	HL,WAIT3
	DEFB	0DDH
RATE5:	LD	HL,WAIT5
	DEFB	0DDH
RATE7:	LD	HL,WAIT7
	DEFB	0DDH
RATE15:	LD	HL,WAIT15
	PUSH	HL
	POP	IX
	BIT	7,B
	JR	Z,MRAMPL

;ＶＲＡＭ上のＰＣＭデータ再生

	POP	HL
	POP	DE
	PUSH	DE

SWVDP1:	LD	C,VDP1
	LD	A,H
	RLA
	RL	E
	RLA
	RL	E
	OUT	(C),E
	LD	A,8EH
	OUT	(C),A
	OUT	(C),L
	LD	A,H
	AND	3FH
	OUT	(C),A

	POP	DE
	LD	B,D
	INC	B
	POP	DE
SWVDP0:	LD	C,VDP0

VPLOOP:	IN	A,(C)
	OUT	(024H),A
	CALL	RWAIT

	DEC	DE
	LD	A,D
	OR	E
	JP	NZ,VPLOOP
	DJNZ	VPLOOP
	EI
	JR	BYE

;メインＲＡＭ上のＰＣＭデータ再生

MRAMPL:	POP	HL
	POP	DE
	POP	BC

MPLOOP:	LD	A,(HL)
	INC	HL
	OUT	(024H),A
	CALL	RWAIT

	DEC	BC
	LD	A,B
	OR	C
	JP	NZ,MPLOOP
	EI

BYE:	LD	A,(CPUMOD)
	LD	C,A
	CALL	CPUCHG

	RET
RWAIT:	JP	(IX)

WAIT3:	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	BIT	7,A
	BIT	7,A
	BIT	7,A
WAIT5:	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	BIT	7,A
	BIT	7,A
	BIT	7,A
WAIT7:	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	INC	DE
	DEC	DE
	INC	DE
	DEC	DE
WAIT15:	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	EX	(SP),HL
	BIT	7,A
	RET

CPUMOD:	NOP

	END
