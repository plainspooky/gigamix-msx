;=== Install Font Driver ' リフィルくん ' ===
;(C)1998 GIGAMIX/Influenza/Itaro
;98/07/07

; リフィルくんがあるかどうか調べる
; A にスロット番号 無ければ 0ffh

; このプログラムはページ3でないと動きません

	.z80
	cseg

	public	RFIL_search
		;[in]  None
		;[out] A : Slot (0ffh = not exist)

SRCHRF::
RFIL_search:
	LD	HL,RFLSCN
	call	slot_scan
	ret
;------------------------
; リフィルくん Search by Influenza
;   4000H 'SHARSHAR!!      '
;[In]	A:Slot#
;[Out]	A:Result

RFLSCN:	PUSH	BC
	PUSH	HL
	PUSH	AF

	ld	h,40h
	call	0024h

	ld	hl,4000h
	ld	de,STRING
	ld	b,16

RFLSCN_loop:
	ld	a,(de)
	cp	(hl)
	jr	nz,false
	inc	hl
	inc	de
	djnz	RFLSCN_loop

	ld	a,(0fcc1h)
	ld	h,40h
	call	0024h

	POP	AF
	POP	HL
	POP	BC
	RET

false:
	ld	a,(0fcc1h)
	ld	h,40h
	call	0024h

	POP	AF
	POP	HL
	POP	BC
	LD	A,0FFH
	RET

STRING:	defb	'SHARSHAR!!      '
;		'0123456789abcdef'
;======================================
; SLOT Search by ITARO
;In:HL=Call ADDR
;Out:A=Slot#(0FFH=Nothing)
;Use:AF,BC,HL
;Memo:	Found...A=Slot#
;	NotFound...A=0FFH

slot_scan:
SLTSCH:	LD	(SLTSC6+1),HL
	LD	(SLTSC7+1),HL
	LD	HL,0FCC1h+3
	LD	B,4
SLTSC1:	LD	A,(HL)
	DEC	HL
	RLCA
	JR	C,SLTSC2
	LD	A,B
	DEC	A
SLTSC6:	CALL	0000
	INC	A
	JR	NZ,SLTSC5
SLTSC4:	DJNZ	SLTSC1
	JR	SLTSC5

SLTSC2:	LD	C,B
	LD	B,4
SLTSC3:	LD	A,B
	DEC	A
	ADD	A,A
	ADD	A,A
	ADD	A,C
	DEC	A
	ADD	A,080H
SLTSC7:	CALL	0000
	INC	A
	JR	NZ,SLTSC5
	DJNZ	SLTSC3
	LD	B,C
	JR	SLTSC4
SLTSC5:	DEC	A
	RET

	end
